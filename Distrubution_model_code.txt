#A model for quantifying root distributions across the active layer profile
#This model is set up to run in BUGS
#This model version looks at the shape of the root distribution
Model{
	for(i in 1:Nobs){
		#likelihood model
		r.bio[i]~dnorm(mu.bio[i],tau.bio)
		#add replicated data for model fit
		r.rep[i]~dnorm(mu.bio[i],tau.bio)
		#likelihood mean
		#based on beta shape and total root biomass
		mu.bio[i]<-r.tot[DaySiteLoc[i]]*Rbeta[i]
		Rbeta[i]<-exp(log.beta[i])
		log.beta[i]<-(log(depth[i])*(alpha[DaySiteLoc[i]]-1))+(log(1-depth[i])*beta[DaySiteLoc[i]])

	}
	
	#priors
	#variance for mean root biomass
	tau.bio<-pow(sig.bio,-2)
	sig.bio~dunif(0,20)
	#priors for beta shape function
	for(i in 1:Ndaysiteloc){
			#start with priors that result in realistic shapes
			#give beta a noninformative prior
			beta[i]~dunif(1,20)
			#try to reduce identifiability issues by using the mode
			#(depth that this highest amount of root biomass occurs at)
			alpha[i]<-((beta[i]*Dmode[i])-(2*Dmode[i])+1)/(1-Dmode[i])
			#read in avergage mode +1 sd and minus 1 sd for prior
			Dmode[i]~dunif(Dlow[i],Dhigh[i])
			#add in some summary stats
			#Dmed[i]<-(alpha[i]-(1/3))/(alpha[i]+beta[i]-(2/3))*Adeep[i]
			#Dmean[i]<-alpha[i]/(alpha[i]+beta[i])*Adeep[i]
			#give r.tot a noninformative prior
			r.tot[i]~dnorm(0,.01)I(0,)
			r.mode[i]<-Dmode[i]*A.depth[i]
			r.mean[i]<-(alpha[i]/(alpha[i]+beta[i]))*A.depth[i]
			r.med[i]<-(alpha[i]-(1/3))/(alpha[i]+beta[i]-(2/3))*A.depth[i]
	}
	


}

#data variables:
#Nobs: number of observations of root biomass
#r.bio: root biomass observation at each depth
#r.tot: total root biomass in the profile (sum of r.bio for each loc and sample depth)
#Day index for sample period varies by root biomass observations
#depth: relative depth in the active layer for each root biomass observation
#Nday: number of sample periods
#Nloc.period
#Site, siteid for biomass observations